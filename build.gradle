apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'

group = 'com.github.jcgay'
version = '0.4-SNAPSHOT'

sourceCompatibility = 1.6
targetCompatibility = 1.6

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'junit', name: 'junit', version: '4.10'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.6.4'
    testRuntime group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.0'
    deployerJars group: 'org.apache.maven.wagon', name: 'wagon-webdav', version: '1.0-beta-2'
}

test {
    include '**/*Test.class'
}

jar {
    manifest {
        attributes("Implementation-Title": rootProject.name, "Implementation-Version": version)
    }
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories.mavenDeployer {
        name = 'cloudbees'
        configuration = configurations.deployerJars
        snapshotRepository(url: "dav:https://repository-jcgay.forge.cloudbees.com/snapshot/") {
            authentication(userName: cloudbeesRepoUser, password: cloudbeesRepoPassword)
        }
        repository(url: "dav:https://repository-jcgay.forge.cloudbees.com/release/") {
            authentication(userName: cloudbeesRepoUser, password: cloudbeesRepoPassword)
        }
    }
}
